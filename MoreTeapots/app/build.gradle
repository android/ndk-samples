apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = "23.0.2"

        defaultConfig.with {
            applicationId    = "com.sample.moreteapots"
            minSdkVersion.apiLevel    = 17
            targetSdkVersion.apiLevel = 22
        }
    }

    android.ndk {
        moduleName = "MoreTeapotsNativeActivity"
        cppFlags.addAll(["-I${file("src/main/jni/cpufeatures")}".toString(),
                         "-I${file("src/main/jni/native_app_glue")}".toString(),
                         "-I${file("src/main/jni/ndk_helper")}".toString()])
        // CFlags is configured in similiar way as cppFlags
        // CFlags.add("-I${file("src/main/jni/native_app_glue")}".toString())
        ldLibs.addAll(["android", "EGL", "GLESv2","log"])
        stl         = "stlport_static"
    }

    android.sources {
        main {
            jni {
                source {
                    srcDirs 'src/main/jni'
                }
            }
        }
    }

    android.productFlavors {
        create ("arm7") {
            ndk.abiFilters.add("armeabi-v7a")
        }
        create ("arm8") {
            ndk.abiFilters.add("arm64-v8a")
        }
        create ("x86-32") {
            ndk.abiFilters.add("x86")
        }
        // for detailed abiFilter descriptions, refer to "Supported ABIs" @
        // https://developer.android.com/ndk/guides/abis.html#sa
        // build one including all productFlavors
        create("all")
    }
}
