apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = '23.0.2'
        defaultConfig {
            applicationId    = 'com.android.gl2jni'
            minSdkVersion.apiLevel    = 5
            targetSdkVersion.apiLevel = 23
        }
        ndk {
            moduleName = 'gl2jni'
            toolchain = 'clang'
            stl        = 'gnustl_static'
            cppFlags.addAll(['-std=c++11','-Wall'])
            ldLibs.addAll(['log', 'GLESv2'])
        }
        // Turn on hard float support in armeabi-v7a
        abis {
            create('armeabi-v7a') {
                cppFlags.addAll(['-mhard-float', '-D_NDK_MATH_NO_SOFTFP=1', '-mfloat-abi=hard'])
                ldLibs.add('m_hard')
                ldFlags.add('-Wl,--no-warn-mismatch')
            }
        }
        buildTypes {
            release {
                minifyEnabled = false
                proguardFiles.add(file('proguard-rules.txt'))
            }
        }
    }
}

