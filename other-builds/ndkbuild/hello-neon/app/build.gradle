apply plugin: 'com.android.application'
// pointing to cmake's source code for the same project
def REMOTE_SRC_ROOT = '../../../../' + rootProject.getName() + '/' +
        project.getName() + '/src/main'

android {
    compileSdkVersion 29
    ndkVersion '22.1.7171670'
    
    defaultConfig {
        applicationId "com.example.helloneon"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                          'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            manifest.srcFile "${REMOTE_SRC_ROOT}/AndroidManifest.xml"
            java.srcDirs = ["${REMOTE_SRC_ROOT}/java"]
            res.srcDirs = ["${REMOTE_SRC_ROOT}/res"]
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'Android.mk'
        }
    }
    flavorDimensions "abi"
    productFlavors {
        arm7 {
            dimension "abi"
            ndk.abiFilters 'armeabi-v7a'
        }
        x86 {
            dimension "abi"
            ndk.abiFilters 'x86'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}
