plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

// pointing to cmake's source code for the same project
def REMOTE_PROJ_ROOT = '../../../../' + rootProject.getName() + '/' +
                        project.getName() + '/src/main'
android {
    compileSdk 33
    ndkVersion '25.1.8937393'

    defaultConfig {
        applicationId "com.example.hellojni"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }
    buildFeatures {
        viewBinding true
    }
    externalNativeBuild {
        ndkBuild {
            path 'Android.mk'
        }
    }
    sourceSets {
        main {
            manifest.srcFile "${REMOTE_PROJ_ROOT}/AndroidManifest.xml"
            java.srcDirs = ["${REMOTE_PROJ_ROOT}/java"]
            res.srcDirs = ["${REMOTE_PROJ_ROOT}/res"]
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                          'proguard-rules.txt'
        }
    }
    namespace 'com.example.hellojni'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.7.21"
}

