apply plugin: 'com.android.model.application'
// pointing to cmake's source code for the same project
def REMOTE_PROJ_ROOT = '../../../../' + rootProject.getName() + '/' +
                        project.getName() + '/src/main'

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = '25.0.0'

        defaultConfig {
            applicationId = 'com.example.hellojni'
            minSdkVersion.apiLevel = 15
            targetSdkVersion.apiLevel = 23
        }
        /*
         * native build settings: taking default for almost everything
         */
        ndk {
            moduleName = 'hello-jni'
            toolchain = 'clang'
            CFlags.addAll(['-Wall'])
        }
        buildTypes {
            release {
                minifyEnabled = false
                proguardFiles.add(file('proguard-rules.txt'))
            }
        }
        sources {
            main {
                manifest {
                    source.srcDir "${REMOTE_PROJ_ROOT}"
                }
                java {
                    source.srcDir "${REMOTE_PROJ_ROOT}/java"
                }
                res {
                    source.srcDir "${REMOTE_PROJ_ROOT}/res"
                }
                jni {
                    source.srcDir "${REMOTE_PROJ_ROOT}/cpp"
                }
            }
        }
        productFlavors {
            // for detailed abiFilter descriptions, refer to "Supported ABIs" @
            // https://developer.android.com/ndk/guides/abis.html#sa
            create("arm7") {
                ndk.abiFilters.add("armeabi-v7a")
            }
            create("arm8") {
                ndk.abiFilters.add("arm64-v8a")
            }
            create("x86") {
                ndk.abiFilters.add("x86")
            }
            create("x86-64") {
                ndk.abiFilters.add("x86_64")
            }
        }
    }
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha7'
}
