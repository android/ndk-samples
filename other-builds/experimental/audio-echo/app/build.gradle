apply plugin: 'com.android.model.application'

// pointing to cmake's source code for the same project
def REMOTE_PROJ_ROOT = '../../../../' + rootProject.getName() + '/' +
        project.getName() + '/src/main'

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = '25.0.3'

        defaultConfig {
            applicationId='com.google.sample.echo'
            minSdkVersion.apiLevel = 17
            targetSdkVersion.apiLevel = 23
            versionCode = 1
            versionName = '1.0'
        }
        ndk {
            platformVersion = 21
            moduleName = 'echo'
            toolchain  = "clang"
            stl        = 'c++_static'   //std::mutex not in c++_static
            cppFlags.add('-std=c++11')
            ldLibs.addAll(['android', 'log', 'OpenSLES', 'atomic'])
            abiFilters.addAll(['armeabi-v7a', 'arm64-v8a', 'x86', 'x86-64'])
        }
        sources {
            main {
                manifest {
                    source.srcDir "${REMOTE_PROJ_ROOT}"
                }
                java {
                    source.srcDir "${REMOTE_PROJ_ROOT}/java"
                }
                res {
                    source.srcDir "${REMOTE_PROJ_ROOT}/res"
                }
                jni {
                    source.srcDir "${REMOTE_PROJ_ROOT}/cpp"
                }
            }
        }
        buildTypes {
            release {
                minifyEnabled=false
                proguardFiles.add(file('proguard-android.txt'))
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
}

