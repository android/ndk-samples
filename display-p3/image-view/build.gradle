apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    ndkVersion '22.1.7171670'

    defaultConfig {
        applicationId = 'com.example.widecolor'
        minSdkVersion   29
        targetSdkVersion 29  // p3_passthrough_ext starts from Android-29
                             // it could be lowered to Android-26, from which p3_ext started
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static', '-DANDROID_PLATFORM=android-29'
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                          'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            version '3.18.1'
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

// download third_party libs: mathfu, stb
List<String> TARGET_DIR = [projectDir.toString() + '/../third_party/mathfu',
                           projectDir.toString() + '/../third_party/stb']
List<List<String>> GIT_PARAMETERS =
       [['clone', '--recursive', 'https://github.com/google/mathfu.git', 'mathfu'],
        ['clone', '--recursive', 'https://github.com/nothings/stb.git', 'stb']]
List<String> WORKING_DIR = [projectDir.toString() + '/../third_party',
                            projectDir.toString() + '/../third_party']
for (int i = 0; i < TARGET_DIR.size(); i++) {
    if (!file(TARGET_DIR[i].toString()).exists()) {
       if(!file(WORKING_DIR[i].toString()).exists())
           file(WORKING_DIR[i].toString()).mkdir()
       exec {
           workingDir(file(WORKING_DIR[i].toString()))
           executable "git"
           args GIT_PARAMETERS[i]
       }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
}
