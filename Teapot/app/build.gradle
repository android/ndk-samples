apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = "23.0.0"

        defaultConfig.with {
            applicationId = "com.sample.teapot"
            minSdkVersion.apiLevel = 17
            targetSdkVersion.apiLevel = 22
        }

    }

    compileOptions.with {
        sourceCompatibility=JavaVersion.VERSION_1_7
        targetCompatibility=JavaVersion.VERSION_1_7
    }

    android.ndk {
        moduleName = "TeapotNativeActivity"
        cppFlags.addAll(["-I${file("src/main/jni/native_app_glue")}".toString(),
	                 "-I${file("src/main/jni/cpufeatures")}".toString(),
                         "-I${file("src/main/jni/ndk_helper")}".toString()])
        // CFlags is configured in similar way as cppFlags
        // CFlags.add("-I${file("src/main/jni/native_app_glue")}".toString())
        ldLibs.addAll(["android", "EGL", "GLESv2", "dl", "log"])
        stl     = "stlport_static"
    }
    // jni is the default dir; config this if yours is in different directory
    android.sources {
        main {
            jni {
                source {
                    srcDirs 'src/main/jni'
                }
            }
        }
    }
    android.buildTypes {
        release {
            minifyEnabled = false
            proguardFiles.add(file('proguard-rules.txt'))
        }
    }
    android.productFlavors {
        create ("arm7") {
            ndk.abiFilters.add("armeabi-v7a")
        }
        create ("arm8") {
            ndk.abiFilters.add("arm64-v8a")
        }
        create ("x86-32") {
            ndk.abiFilters.add("x86")
        }
        // for detailed abiFilter descriptions, refer to "Supported ABIs" @
        // https://developer.android.com/ndk/guides/abis.html#sa
        // build one including all productFlavors
        create("fat")
    }
}
