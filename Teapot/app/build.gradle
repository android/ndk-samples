apply plugin: 'com.android.model.application'

model {
    android {
        compileSdkVersion = 22
        buildToolsVersion = "22.0.1"

        defaultConfig.with {
            applicationId = "com.sample.teapot"
            minSdkVersion.apiLevel = 11
            targetSdkVersion.apiLevel = 22
        }

    }
    android.ndk {
        moduleName = "TeapotNativeActivity"
        cppFlags += "-I${file("src/main/jni/native_app_glue")}".toString()
        cppFlags += "-I${file("src/main/jni/cpufeatures")}".toString()
        cppFlags += "-I${file("src/main/jni/ndk_helper")}".toString()

        // CFlags is configured in similar way as cppFlags
        // CFlags += "-I${file("src/main/jni/native_app_glue")}".toString()
        ldLibs += ["android", "EGL", "GLESv2", "dl", "log"]
        stl     = "stlport_static"
    }
    // jni is the default dir; config this if yours is in different directory
    android.sources {
        main {
            jni {
                source {
                    srcDirs 'src/main/jni'
                }
            }
        }
    }
    android.buildTypes {
        release {
            minifyEnabled = false
            proguardFiles += file('proguard-rules.txt')
        }
    }
    android.productFlavors {
        create ("arm7") {
            ndk.abiFilters += "armeabi-v7a"
        }
        create ("arm8") {
            ndk.abiFilters += "arm64-v8a"
        }
        create ("x86-32") {
            ndk.abiFilters += "x86"
        }
        // for detailed abiFilter descriptions, refer to "Supported ABIs" @
        // https://developer.android.com/ndk/guides/abis.html#sa
        // build one including all productFlavors
        create("fat")
    }
}
