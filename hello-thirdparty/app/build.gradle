apply plugin: 'com.android.model.application'


 def gpg_sdk_path = file(project(':thirdparty:gpg-sdk').projectDir).absolutePath + "/gpg-cpp-sdk/android"

model {
    android {
        compileSdkVersion = 23
        buildToolsVersion = "23.0.0"

        defaultConfig.with {
            //
            //  NOTE: change the application ID to match the play console linked application.
            //
            applicationId = "com.example.android.hello_thirdparty"
            minSdkVersion.apiLevel    = 9
            targetSdkVersion.apiLevel = 23
        }
    }

    compileOptions.with {
        sourceCompatibility=JavaVersion.VERSION_1_7
        targetCompatibility=JavaVersion.VERSION_1_7
    }

    android.ndk {
        moduleName = "native-activity"
        cppFlags += "-I${file("src/main/jni")}".toString()
        cppFlags += "-I${gpg_sdk_path}/include".toString()
        ldLibs += ["log", "android", "EGL", "GLESv1_CM", "gpg", "z"]
        stl    = "c++_static"
    }
    android.buildTypes {
        release {
            minifyEnabled = false
            proguardFiles  += file('proguard-rules.txt')
        }
    }
    android.productFlavors {
        create ("arm7") {
            ndk.abiFilters += "armeabi-v7a"
            ndk.ldFlags += "-L${gpg_sdk_path}/lib/c++/armeabi-v7a".toString()
        }
    }
}


dependencies {
    compile 'com.google.android.gms:play-services-games:7.8.0'
    compile 'com.google.android.gms:play-services-base:7.8.0'
    compile 'com.google.android.gms:play-services-appstate:7.8.0'
    compile 'com.google.android.gms:play-services-nearby:7.8.0'
    compile project(':thirdparty:gpg-sdk')
}

