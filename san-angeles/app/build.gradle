// To demo apk split feature as documented at
// http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits#TOC-ABIs-Splits
plugins {
    id 'com.android.application'
}

import com.android.build.OutputFile
ext.versionCodes = ['armeabi-v7a':1, 'arm64-v8a':2, 'mips':3, 'x86':4]

android {
    compileSdk 33
    ndkVersion '25.1.8937393'

    defaultConfig {
        applicationId 'com.example.SanAngeles'
        minSdkVersion 14
        targetSdkVersion 33
        versionCode  1
        versionName  "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                          'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86'
            universalApk false
        }
    }
    namespace 'com.example.SanAngeles'

    applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(OutputFile.ABI)) * 1000000 +
                            defaultConfig.versionCode
        }
    }
}

